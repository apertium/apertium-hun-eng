## Conversion script between the Apertium stream format and the format fomacg
## expects.

define Stops ["/" | "|" | %" | %^ | %$ ];
define LineSep "| ";
define WhiteSpace [" " | "	"];

define Begin    %^:"$0$ ";
define WordForm 0:{"<} ~$Stops 0:[{>"} " " "#BOC# "];
define NewLine  "/":[LineSep "\u000a"];
define Reading  ~$Stops .o.
                [%> %< <-> " "] .o.
                0:["#0# " %"] ~$"<" "<":[%" " "] ~$"/" ">":" "; 
define Middle   ~$[%^ | %$] .o. WordForm [NewLine Reading]+;
define End      %$:[LineSep "#EOC# " "\u000a"];

define Cohort Begin Middle End;
## The code that feeds Pattern should remove all whitespaces between the
## cohorts. Only in this way can we avoid generation of whitespaces in the
## inverse direction.
# define Pattern [[WhiteSpace:0]* Cohort 0:"\u000a"]+ [WhiteSpace:0]*;
define Pattern [Cohort]+;

read regex Pattern;
save stack apertium_to_fomacg.fst
