# Copyright (c) Dávid Márk Nemeskey

DELIMITERS = "<.>" "<!>" "<?>" "<;>" "<...>" (sent);

#SETS

# Articles
LIST Art    = det ;
LIST DefArt = (det def) ;
LIST IndArt = (det ind) ;
# Demonstratives -- these can also appear in predet position
LIST Dem    = "az" "ez" "amaz" "emez" ;
LIST Noun = n np ;
LIST Verb = vblex vbser vbmod ;
LIST Punct = cm sent quot ;  # guio, apos?
LIST Poss = px1ss px2ss px3ss px1ps px2ps px3ps px1sp px2sp px3sp px1pp px2pp px3pp ;
LIST VbSer = "van" "lehet" "volt" "nincs";

CONSTRAINTS

############
# az       #
############

# Before an ADJ or NOUN, "az" is a <det>
#SELECT (det) IF (0 ("az") LINK 1 (adj) OR (n))
"<az>" SELECT (det) IF (1 (adj) OR (n))
"<Az>" SELECT (det) IF (1 (adj) OR (n))

# Before a definete article, demonstratives are <predet>; they cannot be that
# before an indefinite article (or any other word? So can I use IFF?)
#SELECT (predet) IF (0 Dem LINK 1C DefArt)
#SELECT (predet) IF (0 Dem) (1C DefArt)
#SELECT (predet) + Dem IF (1C DefArt)
#REMOVE (predet) + Dem IF (1C IndArt)
IFF (predet) + Dem IF (1C DefArt)

############
# NP-stuff #
############

# Disambiguate the <det> at the beginning of an NP (<np> too?)
#SELECT (det) IF (1* NOUN BARRIER (*) - AdjOrNum)
SELECT (det) IF (1* Noun BARRIER (*) - (adj) - (num))

## Non-proper nouns need a <det> before them, except if they are a possession
## (e.g. János és kutyája)
#REMOVE (n) IF (NOT 0 Poss) (NOT -1* Art BARRIER (*) - (adj) - (num) - (adv))
# No, they don't -- see e.g. választ kap (collocation), "kutyába se vesz" (idiom), etc.

# No <adv> before <n>. NOT CORRECT. (e.g. before N V constructs)
REMOVE (n) IF (-1C (adv));

# If a noun is written with capital letters and it is not at the start of the
# sentence, it is an np.
SELECT (np) IF (NOT -1 (>>>))

# <plfam> cannot stand with "egy"
REMOVE (plfam) IF (-1* IndArt BARRIER (*) - (adj) - (num) - (adv))

# Note: the rules from this point are based on the rules of the Hungarian NP
# grammar. Unfortunately, there are some points, where different analyses are
# possible (e.g. can an inflected <adj> + a zero <n> be the head of an NP?).
# The rules here are based on my decision on how NPs look like.

# <adj>s cannot take <pxNPP>. Examples such as "(tojás)fehérje" do not refer
# to the property of the noun, but rather, to one of its parts.
REMOVE (adj) + Poss IF (0 Noun + Poss)

#########
# Verbs #
#########

# Remove the <vblex> analysis of <vbser>s, erroneously added by hunmorph.
REMOVE VbSer + (vblex) + (pres)

# pis is imperative (and also very rare)
REMOVE (pis) IF (NOT *1 ("!"))

#########
# prevb #
#########

# In case of verbs modified by a preverb, hunmorph returns both the
# (underspecified) vblex/ser and the prevb + vblex/ser analysis. Of these, we
# select the latter one.
# Note: this rule is not really sound because of two reasons: first, the word
#       form of a verb with a preverb might be the same as that of a regular
#       verb, though this is rare. Second, this kind of disambiguation is
#       really the morphological analyser's task: hunmorph should not return
#       the underspecified analysis, if it knows better (and it does).
# For sub-readings, see http://beta.visl.sdu.dk/cg3/chunked/subreadings.html
#SELECT (prevb) + (vblex) IF (0C Verb LINK 0:1 (prevb))
SELECT SUB:1 (prevb) IF (0C Verb)

# preverbs cannot occur before a verb (in that case, they must form a compound
# word), except if there is an infinite verb somewhere later in the sentence
# (e.g. el<prevb> akarja<vblex> olvasni<vblex><inf>).
REMOVE (prevb) IF (1C Verb) (NOT 2* Verb + (inf)) 

##########
# cnjcoo #
##########

# cnjcoo is always at the beginning of the second part, never at the end of the
# first one.
REMOVE (cnjcoo) IF (1 Punct)

# "de" is <adv> only immediately before and <adj> (e.g. de hideg van)
REMOVE ("de" adv) IF (NOT 1C (adj))

#########
#  nem  #
#########

# "nem" is always adv before a verb. (TODO: but after another ij?)
IFF ("nem" adv) IF (1C Verb OR (<adj>) OR Noun)

# TODO: Nem, Marinak: ij?

########
# MISC #
########

# Remove the archaic "fő" reading when "fej" is available
REMOVE ("fő") IF (0 ("fej"))

# Remove the familiar form "anyjuk", if it is not preceded by an <ij>
REMOVE ("anyjuk") IF (NOT -1* (ij) BARRIER (cm))

########
# ???  #
########

# E.g. ADDs for <adj>: <pred>/<attr>
